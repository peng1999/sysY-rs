use std::str::FromStr;

use crate::ast;

grammar;

pub Stmt: ast::Stmt = {
    StmtOther,
    "if" "(" <c:Expr> ")" <t:Stmt> => ast::Stmt::If(Box::new(c), Box::new(t), Box::new(ast::Stmt::Empty)),
    "if" "(" <c:Expr> ")" <t:WithElse> "else" <f:Stmt> => ast::Stmt::If(Box::new(c), Box::new(t), Box::new(f)),
}

WithElse = {
    StmtOther,
    "if" "(" <c:Expr> ")" <t:WithElse> "else" <f:WithElse> => ast::Stmt::If(Box::new(c), Box::new(t), Box::new(f)),
}

StmtOther: ast::Stmt = {
    Decl,
    <Expr> ";" => ast::Stmt::Expr(<>),
    <l:Expr> "=" <r:Expr> ";" => ast::Stmt::Assign(Box::new(l), Box::new(r)),
    "{" <Stmt*> "}" => ast::Stmt::Block(<>),
    ";" => ast::Stmt::Empty,
    "return" <v:Expr?> ";" => ast::Stmt::Return(v),
}

Decl: ast::Stmt = {
    <t:Ident> <n:Ident> "=" <v:Expr> ";" => ast::Stmt::Decl(t, n, Box::new(v)),
}

Expr: ast::Expr = AddExpr;

AddExpr = {
    MulExpr,
    <l:AddExpr> <op:AddOp> <r:MulExpr> => ast::Expr::Binary(op, Box::new(l), Box::new(r)),
}

MulExpr = {
    UnaryExpr,
    <l:MulExpr> <op:MulOp> <r:UnaryExpr> => ast::Expr::Binary(op, Box::new(l), Box::new(r)),
}

UnaryExpr = {
    <op:UnaryOp> <e:UnaryExpr>              => ast::Expr::Unary(op, Box::new(e)),
    <name:IdentExpr> "(" <p:ParamList> ")"  => ast::Expr::Call(Box::new(name), p),
    <name:IdentExpr> "(" ")"                => ast::Expr::Call(Box::new(name), vec![]),
    <name:IdentExpr> <p:IndexList>          => ast::Expr::Index(Box::new(name), p),
    Scalar,
}

ParamList: Vec<ast::Expr> = {
    Expr => vec![<>],
    <mut p:ParamList> "," <e:Expr> => { p.push(e); p },
}

IndexList: Vec<ast::Expr> = {
    "[" <Expr> "]"                      => vec![<>],
    <mut p:IndexList> "[" <e:Expr> "]"  => { p.push(e); p },
}

Scalar = {
    NumLit,
    IdentExpr,
    "(" <e:Expr> ")" => e,
}

IdentExpr: ast::Expr = Ident => ast::Expr::Ident(<>);

//
// Tokens
//

NumLit: ast::Expr = r"[0-9]+" => ast::Expr::Lit(i32::from_str(<>).unwrap());
Ident: String = r"[a-zA-Z][a-zA-Z0-9]*" => <>.to_owned();

UnaryOp: ast::UnOp = {
    "+" => ast::UnOp::Pos,
    "-" => ast::UnOp::Neg,
    "!" => ast::UnOp::Not,
}

MulOp: ast::BinOp = {
    "*" => ast::BinOp::Mul,
    "/" => ast::BinOp::Div,
    "%" => ast::BinOp::Rem,
}

AddOp: ast::BinOp = {
    "+" => ast::BinOp::Add,
    "-" => ast::BinOp::Sub,
}
