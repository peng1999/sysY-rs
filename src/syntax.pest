Prog = _{ SOI ~ Block ~ EOI }

Block = _{ "{" ~ (Decl | Stat)* ~ "}" }

Decl = _{ VarDecl }

VarDecl = { BType ~ VarDef ~ ("," ~ VarDef)* }
VarDef = { Ident ~ ("=" ~ InitVal)? }
InitVal = _{ Expr }

Stat = _{
  (AssignStat
  | BreakStat
  | Expr
  )?
  ~ ";"
}

AssignStat = { LVal ~ "=" ~ Expr }
BreakStat = { "break" }

LVal = _{ SubTerm | Ident }

Expr = _{ AddExpr }
AddExpr = _{ AddTerm | MulExpr }
MulExpr = _{ MulTerm | UnaryExpr }
UnaryExpr = _{ CallTerm | SubTerm | UnaryTerm | Scalar }
Scalar = _{ NumLit | Ident | "(" ~ Expr ~ ")" }

AddTerm = { MulExpr ~ (AddOp ~ MulExpr)+ }
MulTerm = { UnaryExpr ~ (MulOp ~ UnaryExpr)+ }
UnaryTerm = { UnaryOp ~ UnaryExpr }
CallTerm = { Ident ~ "(" ~ (Expr ~ ("," ~ Expr)*)? ~ ")" }
SubTerm = { Ident ~ ("[" ~ Expr ~ "]")+ }

BType = { "int" }

NumLit = @{ ASCII_DIGIT+ }
Ident = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

AddOp = { "-" | "+" }
MulOp = { "*" | "/" | "%" }
UnaryOp = { "+" | "-" | "*" }

WHITESPACE = _{ " " | "\t" | "\n" }
